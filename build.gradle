import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenCentral() // For the ProGuard Gradle Plugin and anything else.
    }
    dependencies {
        classpath group: 'com.guardsquare', name: 'proguard-gradle', version: '7.2.0-beta2'
    }
}

plugins {
    id 'java'
}

group = project.plugin_group
version = project.plugin_version
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    implementation group: 'org.spigotmc', name: 'spigot-api', version: '1.17.1-R0.1-SNAPSHOT'
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

processResources {
    def props = [version: project.plugin_version, group: project.plugin_group, name: project.plugin_name, author: project.plugin_author, website: project.plugin_website]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

def proguardOutFile = rootProject.file("$buildDir/libs/release.jar")
def jarFile = jar.outputs.files.singleFile
task proguard(type: ProGuardTask) {
    outputs.upToDateWhen { false }
    configuration 'configuration.pro'

    injars jarFile
    outjars proguardOutFile
    libraryjars configurations.findByName('runtimeClasspath').getFiles()
    // Java 8
    libraryjars "${System.getProperty("java.home")}/lib/rt.jar"
    // Java 9+ compatibility
    libraryjars "${System.getProperty("java.home")}/jmods/java.base.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/java.desktop.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/java.logging.jmod"
    libraryjars "${System.getProperty("java.home")}/jmods/jdk.unsupported.jmod"
}

jar.finalizedBy proguard
proguard.doLast {
    if (!jarFile.delete()) throw new IllegalStateException()
    if (!proguardOutFile.renameTo(jarFile)) throw new IllegalStateException()
}